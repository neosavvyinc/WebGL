<script src="../js/gl_utils.js"></script>

<body bgcolor=black>
<canvas id='canvas1' width='800' height='800'>
</canvas>
</body>

<script id="vs" type="x-shader/x-vertex">
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision mediump float;
    uniform float uTime;
    varying vec3 vPosition;
    uniform vec3 uCursor;

    struct Sphere {
        vec3 center;
        float radius;
        vec3 color;
    };

    struct Ray {
        vec3 origin;
        vec3 direction;
    };

    struct Light {
        vec3 position;
        float ambience;
        vec3 specular;
        vec3 diffuse;
    };

    Sphere spheres[1];
    Ray rays[1];
    Light light[1];

    void initialize() {
        float x = vPosition.x;
        float y = vPosition.y;
        float z = vPosition.z;
        float focalLength = 2.0;
        vec3 color = vec3(0.0, 0.0, 0.0);

        // Create spheres
        spheres[0].center = vec3(0.0, 0.0, 0.0);
        spheres[0].radius = 0.7;
        spheres[0].color = vec3(1.0, 0.0, 0.0);

        // Create ray
        rays[0].origin = vec3(0.0, 0.0, 4.0);
        rays[0].direction = normalize(vec3(x-0.5, 0.5-y, -focalLength));

        // Create Light source
        light[0].position = vec3(uCursor.x, -uCursor.y, 0.9);
        light[0].ambience = 0.01;
    }

    vec3 checkIntersectSphere(Sphere sphere, Ray ray, Light light) {
        vec3 sphereCenter = sphere.center;
        vec3 colorOfSphere = sphere.color;
        float radius = sphere.radius;
        vec3 cameraSource = ray.origin;
        vec3 cameraDirection = ray.direction;
        vec3 lightSource = light.position;
        float ambience = light.ambience;
        vec3 color = vec3(0.0, 0.0, 0.0);

        vec3 distanceFromCenter = (cameraSource - sphereCenter);
        float B = 2.0 * dot(cameraDirection, distanceFromCenter);
        float C = dot(distanceFromCenter, distanceFromCenter) - pow(radius, 2.0);
        float delta = pow(B, 2.0) - 4.0 * C;
        float t = 0.0;
        if (delta > 0.0) {
            float sqRoot = sqrt(delta);
            float t1 = (-B + sqRoot) / 2.0;
            float t2 = (-B - sqRoot) / 2.0;
            t = min(t1, t2);
        }
        if (delta == 0.0) {
            t = -B / 2.0;
        }

        if (t > 0.0) {
            vec3 surfacePoint = cameraSource + (t * cameraDirection);
            vec3 surfaceNormal = normalize(surfacePoint - sphereCenter);
            color = colorOfSphere * (ambience + ((1.0 - ambience) * max(0.0, dot(surfaceNormal, lightSource))));
        }
        return color;
    }

    void main() {
        initialize();
        vec3 color = checkIntersectSphere(spheres[0], rays[0], light[0]);
        gl_FragColor = vec4(color, 1.0);
   }
</script>

<script>
    start_gl("canvas1", getStringFromDOMElement('vs'), getStringFromDOMElement('fs'));
</script>

